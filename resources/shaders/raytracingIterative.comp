#version 430 core

//Complete implementation of the recursive CPU raytracer. Not working that well sadly :(

struct Material {
    vec4 color;
    vec4 modifiers;  //First value is reflectivness, second one is transparency
};

struct Sphere {
    vec4 position;
    vec4 radius;
    Material material;
};

struct MeshObject {
    vec4 vertexData[1024];
    mat4 modelMatrix;
    vec4 numTris;
    Material material;
};

struct PointLight {
    vec4 position;
    vec4 color;  //Last vlaue is intensity
};

struct Camera {
    mat4 modelMatrix;
};

struct SceneData {
    MeshObject meshObjects[4];
    Sphere spheres[4];
    PointLight pointLights[4];
    Camera camera;
    int numMeshObjects;
    int numSpheres;
    int numPointLights;
    int screenWidth;
    int screenHeight;
};

struct IntersectionData {
    Material material;
    vec3 position;
    vec3 normal;
    bool hit;
};

struct Ray {
    vec3 origin;
    vec3 direction;
};

struct RTNode {
    IntersectionData intersectionData;
    Ray reflectionRay;
    vec3 reflectionColor;
    bool hasReflectionRay;
    Ray refractionRay;
    vec3 refractionColor;
    bool hasRefractionRay;
    float fresnel;
    vec3 combinedColor;
};

layout(local_size_x = 4, local_size_y = 4) in;
layout(rgba32f, binding = 0) uniform image2D img_output;
layout(std430, binding = 1) buffer sceneDataBuffer {
    MeshObject meshObjects[4];
    Sphere spheres[4];
    PointLight pointLights[4];
    Camera camera;
    int numMeshObjects;
    int numSpheres;
    int numPointLights;
    int screenWidth;
    int screenHeight;
};

vec3 raytracePixel(vec3 origin, vec3 dir);
RTNode raytraceRay(Ray ray, int depth);
IntersectionData raycastObjects(vec3 origin, vec3 dir);

void main() {
    vec4 pixelColor = vec4(1.0, 0.0, 0.0, 1.0);

    //Constants and definitions
    const float pi = 3.14159f;

    //Computing ray direction
    float invWidth = 1 / float(screenWidth), invHeight = 1 / float(screenHeight);
    float fov = 30;
    float aspectratio = screenWidth / float(screenHeight);
    float angle = tan(pi * 0.5 * fov / 180.0f);
    float xx = (2 * ((float(gl_GlobalInvocationID.x) + 0.5) * invWidth) - 1) * angle * aspectratio;
    float yy = -(1 - 2 * ((float(gl_GlobalInvocationID.y) + 0.5) * invHeight)) * angle;  //Y-Axis is flipped

    //Calculating the camera ray with camera transformations in mind
    vec4 camPosWorld = camera.modelMatrix * vec4(0, 0, 0, 1);
    vec4 projectionRayWorld = camera.modelMatrix * vec4(xx, yy, 1, 1);
    vec4 projectionRayDirWorld = projectionRayWorld - camPosWorld;
    projectionRayDirWorld = normalize(projectionRayDirWorld);

    //Actual raytracing
    pixelColor.xyz = raytracePixel(camPosWorld.xyz, projectionRayDirWorld.xyz);
    //pixelColor.xyz = raytracePixel(vec3(0, 0, -15), vec3(0, 0, 1));

    ivec2 pixel_coords = ivec2(gl_GlobalInvocationID.xy);
    imageStore(img_output, pixel_coords, pixelColor);
}

RTNode raytraceRay(Ray ray, int depth) {
    IntersectionData intersect = raycastObjects(ray.origin, ray.direction);
    RTNode node;
    node.intersectionData = intersect;
    node.combinedColor = vec3(0, 0, 0);
    node.fresnel = 0;
    node.hasReflectionRay = false;
    node.hasRefractionRay = false;

    if (node.intersectionData.hit) {
        if ((intersect.material.modifiers.x > 0.0f || intersect.material.modifiers.y > 0.0f) && depth < 5) {
            //Reflective and refractive color computation
            bool inObject = false;
            vec3 norm = intersect.normal;
            if (dot(ray.direction, norm) > 0) {
                norm = -norm;
                inObject = true;
            }

            float facingRatio = -dot(ray.direction, norm);
            float fresnel = mix(pow(1 - facingRatio, 3), 1.0f, 0.1f);
            node.fresnel = fresnel;

            vec3 reflectionDir = ray.direction - norm * 2.0f * dot(ray.direction, norm);
            reflectionDir = normalize(reflectionDir);

            vec3 newRayOrigin = intersect.position + norm * 0.0001f;

            Ray reflectionRay;
            reflectionRay.origin = newRayOrigin;
            reflectionRay.direction = reflectionDir;

            node.reflectionRay = reflectionRay;
            node.hasReflectionRay = true;

            if (intersect.material.modifiers.y > 0.0f) {
                float ior = 1.1f;  //Index of refraction
                float eta = ior;
                if (!inObject) {
                    eta = 1.0f / ior;
                }
                float cosi = -dot(ray.direction, norm);
                float k = 1 - eta * eta * (1 - cosi * cosi);
                vec3 refractionDir = ray.direction * eta + norm * (eta * cosi - sqrt(k));
                refractionDir = normalize(refractionDir);
                newRayOrigin = intersect.position - norm * 0.0001f;

                Ray refractionRay;
                refractionRay.origin = newRayOrigin;
                refractionRay.direction = refractionDir;
                node.refractionRay = refractionRay;
                node.hasRefractionRay = true;
            }
        } else {
            //Diffuse color computation
            for (int i = 0; i < numPointLights; i++) {
                float dst = distance(intersect.position, pointLights[i].position.xyz);
                if (dst < 1.0f) {
                    dst = 1.0f;
                }

                vec3 shadowRayDir = pointLights[i].position.xyz - intersect.position;
                vec3 norm = intersect.normal;
                shadowRayDir = normalize(shadowRayDir);

                vec3 newRayOrigin = intersect.position + norm * 0.0001f;
                IntersectionData shadowIntersect = raycastObjects(newRayOrigin, shadowRayDir);

                if (!shadowIntersect.hit) {
                    //Diffuse color calculation. Light intensity uses the inverse square law with a scaling modifier
                    node.combinedColor += intersect.material.color.xyz * 1.0f * max(0.0f, dot(norm, shadowRayDir)) * pointLights[i].color.xyz * pointLights[i].color.w * 1.0f / pow(dst, 0.01f);
                }
            }
        }
    }
    return node;
}

vec3 raytracePixel(vec3 origin, vec3 dir) {
    //Constants and definitions
    const int maxDepth = 5;
    const int maxNodes = 63;  //Basically pow(2, maxDepth + 1) - 1

    RTNode raytracingArray[maxNodes];
    for (int i = 0; i < maxNodes; i++) {
        raytracingArray[i].combinedColor = vec3(0, 0, 0);
        raytracingArray[i].fresnel = 0;
        raytracingArray[i].hasReflectionRay = false;
        raytracingArray[i].hasRefractionRay = false;
    }

    //Build raytracing tree structure
    for (int i = 0; i < maxNodes; i++) {
        int parentIndex = int(floor((i - 1) / 2.0f));
        int depth = int(floor(log2(i + 1)));
        if (parentIndex >= 0) {
            RTNode parent = raytracingArray[parentIndex];
            if (i == 2 * parentIndex + 1 && parent.hasReflectionRay) {
                raytracingArray[i] = raytraceRay(raytracingArray[parentIndex].reflectionRay, depth);
            } else if (i == 2 * parentIndex + 2 && parent.hasRefractionRay) {
                raytracingArray[i] = raytraceRay(raytracingArray[parentIndex].refractionRay, depth);
            }
        } else {
            Ray ray;
            ray.origin = origin;
            ray.direction = dir;
            raytracingArray[i] = raytraceRay(ray, depth);
        }
    }

    //Backtrack from last node to the first
    for (int i = maxNodes - 1; i > 0; i--) {
        int parentIndex = int(floor((i - 1) / 2.0f));
        RTNode parent = raytracingArray[parentIndex];
        RTNode current = raytracingArray[i];

        //Is leaf or inner node?
        if (i >= pow(2, maxDepth) - 1) {
            if (2 * parentIndex + 1 == i && parent.hasReflectionRay) {
                parent.reflectionColor = current.combinedColor;
            } else if (2 * parentIndex + 2 == i && parent.hasRefractionRay) {
                parent.refractionColor = current.combinedColor;
            }
        } else {
            if (current.hasReflectionRay || current.hasRefractionRay) {
                current.combinedColor = (current.reflectionColor * current.fresnel * current.intersectionData.material.modifiers.x + current.refractionColor * (1 - current.fresnel) * current.intersectionData.material.modifiers.y) * current.intersectionData.material.color.xyz;
            }

            if (2 * parentIndex + 1 == i && parent.hasReflectionRay) {
                parent.reflectionColor = current.combinedColor;
            } else if (2 * parentIndex + 2 == i && parent.hasRefractionRay) {
                parent.refractionColor = current.combinedColor;
            }
        }
        raytracingArray[parentIndex] = parent;
        raytracingArray[i] = current;
    }
    RTNode current = raytracingArray[0];
    if (current.hasReflectionRay || current.hasRefractionRay) {
        current.combinedColor = (current.reflectionColor * current.fresnel * current.intersectionData.material.modifiers.x + current.refractionColor * (1 - current.fresnel) * current.intersectionData.material.modifiers.y) * current.intersectionData.material.color.xyz;
    }
    raytracingArray[0] = current;
    return raytracingArray[0].combinedColor;
}

IntersectionData raycastObjects(vec3 origin, vec3 rayDir) {
    IntersectionData closestIntersect;
    closestIntersect.hit = false;
    closestIntersect.position = vec3(0, 0, 0);
    float dst = 9999999.0f;

    //Mesh raycasting
    for (int m = 0; m < numMeshObjects; m++) {
        for (int i = 0; i < meshObjects[m].numTris.x * 3; i += 3) {
            //Möller–Trumbore intersection algorithm
            vec3 vertex0 = (meshObjects[m].modelMatrix * meshObjects[m].vertexData[i]).xyz;
            vec3 vertex1 = (meshObjects[m].modelMatrix * meshObjects[m].vertexData[i + 1]).xyz;
            vec3 vertex2 = (meshObjects[m].modelMatrix * meshObjects[m].vertexData[i + 2]).xyz;

            vec3 edge1 = vertex1 - vertex0;
            vec3 edge2 = vertex2 - vertex0;

            vec3 pvec = cross(rayDir, edge2);

            float det = dot(edge1, pvec);
            float epsilon = 0.000001f;

            if (det < epsilon) {
                continue;
            }

            float invDet = 1.0f / det;

            vec3 tvec = origin - vertex0;

            float x = dot(tvec, pvec) * invDet;
            if (x < 0.0f || x > 1.0f) {
                continue;
            }

            vec3 qvec = cross(tvec, edge1);
            float y = dot(rayDir, qvec) * invDet;
            if (y < 0.0f || x + y > 1.0f) {
                continue;
            }

            float z = dot(edge2, qvec) * invDet;

            vec3 intersect = origin + rayDir * z;
            vec3 norm = cross(vertex0 - vertex1, vertex0 - vertex2);
            norm = normalize(norm);

            if (z < epsilon) {
                continue;
            }

            if (distance(intersect, origin) < dst) {
                dst = distance(intersect, origin);
                closestIntersect = IntersectionData(meshObjects[m].material, intersect, norm, true);
            }
        }
    }

    //Sphere raycasting
    for (int s = 0; s < numSpheres; s++) {
        vec3 centerOrigin = spheres[s].position.xyz - origin;
        float tco = dot(centerOrigin, rayDir);
        if (tco < 0) {
            continue;
        }
        float dstToCenterSqr = dot(centerOrigin, centerOrigin) - (tco * tco);
        float radiusSquared = spheres[s].radius.x * spheres[s].radius.x;
        if (dstToCenterSqr > radiusSquared) {
            continue;
        }
        float thc = sqrt(radiusSquared - dstToCenterSqr);
        float th0 = tco - thc;
        float th1 = tco + thc;
        if (th0 > th1) {
            float temp = th0;
            th0 = th1;
            th1 = temp;
        }
        if (th0 < 0) {
            th0 = th1;
            if (th0 < 0) {
                continue;
            }
        }
        vec3 hitPos = origin + th0 * rayDir;
        vec3 normal = normalize((hitPos - (spheres[s].position.xyz) / spheres[s].radius.x));

        if (distance(hitPos, origin) < dst) {
            dst = distance(hitPos, origin);
            closestIntersect = IntersectionData(spheres[s].material, hitPos, normal, true);
        }
    }
    return closestIntersect;
}